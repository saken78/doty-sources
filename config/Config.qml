pragma Singleton
pragma ComponentBehavior: Bound
import QtQuick
import Quickshell
import Quickshell.Io

Singleton {
    id: root

    FileView {
        id: loader
        path: Qt.resolvedUrl("./config.json")
        preload: true
        watchChanges: true
        onFileChanged: reload()

        adapter: JsonAdapter {
            property JsonObject theme: JsonObject {
                property bool oledMode: false
                property real opacity: 1.0
                property int roundness: 16
                property string defaultFont: "Roboto Condensed"
                property int animDuration: 300
            }

            property JsonObject bar: JsonObject {
                property string position: "top"
                property string launcherIcon: ""
                property string overviewIcon: ""
                property bool showBackground: false
                property bool verbose: true
                property list<string> screenList: []
            }

            property JsonObject workspaces: JsonObject {
                property int shown: 10
                property bool showAppIcons: true
                property bool alwaysShowNumbers: false
                property bool showNumbers: false
            }

            property JsonObject overview: JsonObject {
                property int rows: 2
                property int columns: 5
                property real scale: 0.1
                property real workspaceSpacing: 8
            }
        }
    }

    // Theme configuration
    property bool oledMode: loader.adapter.theme.oledMode
    property real opacity: Math.min(Math.max(loader.adapter.theme.opacity, 0.1), 1.0)
    property int roundness: loader.adapter.theme.roundness
    property string defaultFont: loader.adapter.theme.defaultFont
    property int animDuration: loader.adapter.theme.animDuration

    // Bar configuration
    property QtObject bar: loader.adapter.bar

    // Workspace configuration
    property QtObject workspaces: loader.adapter.workspaces

    // Overview configuration
    property QtObject overview: loader.adapter.overview
}
