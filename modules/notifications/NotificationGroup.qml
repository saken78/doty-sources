import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Quickshell
import Quickshell.Widgets
import Quickshell.Services.Notifications
import qs.modules.theme
import qs.modules.services
import qs.config
import "./NotificationAnimation.qml"
import "./notification_utils.js" as NotificationUtils

Item {
    id: root
    property var notificationGroup
    property var notifications: notificationGroup?.notifications ?? []
    property int notificationCount: notifications.length
    property bool multipleNotifications: notificationCount > 1
    property var validNotifications: notifications.filter(n => n != null && n.summary != null)

    onNotificationGroupChanged: {
        console.log("[GROUP-DEBUG] Grupo de notificaciones cambió:", {
            appName: notificationGroup?.appName,
            totalNotifications: notifications.length,
            validNotifications: validNotifications.length,
            notifications: notifications.map(n => ({
                        id: n?.id,
                        summary: n?.summary,
                        body: n?.body
                    }))
        });
    }

    onValidNotificationsChanged: {
        console.log("[GROUP-DEBUG] Notificaciones válidas cambiaron:", {
            appName: notificationGroup?.appName,
            count: validNotifications.length,
            validIds: validNotifications.map(n => n?.id)
        });
    }
    property bool expanded: false
    property bool popup: false
    property real padding: 16
    implicitHeight: background.implicitHeight

    property real dragConfirmThreshold: 70
    property real dismissOvershoot: 20
    property var qmlParent: root.parent.parent
    property var parentDragIndex: qmlParent?.dragIndex ?? -1
    property var parentDragDistance: qmlParent?.dragDistance ?? 0
    property var dragIndexDiff: Math.abs(parentDragIndex - (index ?? 0))
    property real xOffset: dragIndexDiff == 0 ? Math.max(0, parentDragDistance) : parentDragDistance > dragConfirmThreshold ? 0 : dragIndexDiff == 1 ? Math.max(0, parentDragDistance * 0.3) : dragIndexDiff == 2 ? Math.max(0, parentDragDistance * 0.1) : 0

    function destroyWithAnimation() {
        if (root.qmlParent && root.qmlParent.resetDrag)
            root.qmlParent.resetDrag();
        background.anchors.leftMargin = background.anchors.leftMargin;
        notificationAnimation.startDestroy();
    }

    NotificationAnimation {
        id: notificationAnimation
        targetItem: background
        dismissOvershoot: root.dismissOvershoot
        parentWidth: root.width

        onDestroyFinished: {
            root.notifications.forEach(notif => {
                Qt.callLater(() => {
                    Notifications.discardNotification(notif.id);
                });
            });
        }
    }

    function toggleExpanded() {
        root.expanded = !root.expanded;
    }

    // Escuchar cuando las notificaciones van a hacer timeout
    Connections {
        target: Notifications
        function onTimeoutWithAnimation(id) {
            // Verificar si la notificación que va a hacer timeout pertenece a este grupo
            const notifExists = root.notifications.some(notif => notif.id === id);
            if (notifExists && root.popup) {
                root.destroyWithAnimation();
            }
        }
    }

    // HoverHandler dedicado para pausar/reanudar timers
    HoverHandler {
        id: hoverHandler

        onHoveredChanged: {
            if (hovered) {
                if (notificationGroup?.appName) {
                    Notifications.pauseGroupTimers(notificationGroup.appName);
                }
            } else {
                if (notificationGroup?.appName) {
                    Notifications.resumeGroupTimers(notificationGroup.appName);
                }
            }
        }
    }

    MouseArea {
        id: dragManager
        anchors.fill: parent
        acceptedButtons: Qt.LeftButton | Qt.RightButton | Qt.MiddleButton

        onClicked: mouse => {
            if (mouse.button === Qt.RightButton)
                root.toggleExpanded();
            else if (mouse.button === Qt.MiddleButton)
                root.destroyWithAnimation();
        }

        property bool dragging: false
        property real dragDiffX: 0

        function resetDrag() {
            dragging = false;
            dragDiffX = 0;
        }
    }

    ClippingRectangle {
        id: background
        anchors.left: parent.left
        width: parent.width
        color: Colors.background
        radius: Config.roundness > 0 ? Config.roundness : 0
        border.color: Colors.surfaceContainerHigh
        border.width: 0
        anchors.leftMargin: root.xOffset

        Behavior on anchors.leftMargin {
            enabled: !dragManager.dragging
            NumberAnimation {
                duration: 300
                easing.type: Easing.OutCubic
            }
        }

        clip: true
        implicitHeight: expanded ? row.implicitHeight + padding * 2 : Math.max(56 + padding * 2, row.implicitHeight + padding * 2)

        Behavior on implicitHeight {
            NumberAnimation {
                duration: Config.animDuration
                easing.type: Easing.OutBack
            }
        }

        RowLayout {
            id: row
            anchors.top: parent.top
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.margins: root.padding
            spacing: root.padding / 2

                ColumnLayout {
                    Layout.fillWidth: true
                    spacing: root.notificationCount === 1 ? 0 : 8

                    Behavior on spacing {
                        NumberAnimation {
                            duration: Config.animDuration
                            easing.type: Easing.OutCubic
                        }
                    }



                Item {
                    id: topRow
                    Layout.fillWidth: true
                     property real fontSize: Config.theme.fontSize
                    property bool showAppName: root.multipleNotifications
                    implicitHeight: root.multipleNotifications ? Math.max(topTextRow.implicitHeight, expandButton.implicitHeight) : 0
                    visible: root.multipleNotifications

                    RowLayout {
                        id: topTextRow
                        anchors.left: parent.left
                        anchors.right: expandButton.left
                        anchors.verticalCenter: parent.verticalCenter
                        spacing: 5
                        visible: root.multipleNotifications

                        Text {
                            id: appName
                            elide: Text.ElideRight
                            Layout.fillWidth: true
                             text: (topRow.showAppName ? notificationGroup?.appName : (root.validNotifications.length > 0 ? root.validNotifications[0]?.summary ?? "" : "")) || ""
                             font.family: Config.theme.font
                             font.pixelSize: Config.theme.fontSize
                             font.weight: Font.Bold
                             color: topRow.showAppName ? Colors.adapter.outline : Colors.adapter.primary
                        }
                        Text {
                            id: timeText
                            Layout.rightMargin: 10
                            horizontalAlignment: Text.AlignLeft
                             text: NotificationUtils.getFriendlyNotifTimeString(notificationGroup?.time)
                             font.family: Config.theme.font
                             font.pixelSize: Config.theme.fontSize
                             color: Colors.adapter.overBackground
                        }
                    }
                    NotificationGroupExpandButton {
                        id: expandButton
                        anchors.right: parent.right
                        anchors.verticalCenter: parent.verticalCenter
                        count: root.notificationCount
                        expanded: root.expanded
                        fontSize: topRow.fontSize
                        visible: root.multipleNotifications
                        onClicked: {
                            root.toggleExpanded();
                        }
                    }
                }

                ListView {
                    id: notificationsColumn
                    implicitHeight: contentHeight
                    Layout.fillWidth: true
                    spacing: 4
                    interactive: false

                    Behavior on spacing {
                        NumberAnimation {
                            duration: Config.animDuration
                            easing.type: Easing.OutCubic
                        }
                    }



                    model: expanded ? root.validNotifications.slice().reverse() : root.validNotifications.slice().reverse().slice(0, 2)

                    delegate: NotificationItem {
                        required property int index
                        required property var modelData
                        notificationObject: modelData
                        expanded: root.expanded
                        onlyNotification: (root.notificationCount === 1)
                        opacity: (!root.expanded && index == 1 && root.notificationCount > 2) ? 0.5 : 1
                        visible: root.expanded || (index < 2)
                        anchors.left: parent?.left
                        anchors.right: parent?.right

                        Component.onCompleted: {
                            console.log("[GROUP-DEBUG] NotificationItem creado:", {
                                index: index,
                                id: modelData?.id,
                                summary: modelData?.summary,
                                body: modelData?.body,
                                isValid: modelData != null && (modelData.summary != null || modelData.body != null)
                            });
                        }

                        onDestroyRequested: {
                            if (root.notificationCount === 1) {
                                root.destroyWithAnimation();
                            }
                        }
                    }
                }
            }
        }
    }
}
